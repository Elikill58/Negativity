import com.elikill58.negativity.build.spigot.SpigotReobfPlugin
import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'com.github.johnrengelman.shadow'
    id 'negativity-spigot-reobf'
}

sourceSets {
    main {
        java.srcDirs = ['src']
        resources.srcDirs = ['resources']
    }
}

repositories {
    mavenLocal {
        metadataSources {
            mavenPom()
            artifact()
        }
        content {
            includeGroup('org.spigotmc')
        }
    }
}

java.toolchain.languageVersion = JavaLanguageVersion.of(16)

dependencies {
    api parent

    SpigotReobfPlugin.configureSpigotRemapDeps(project, '1.17-R0.1-SNAPSHOT', '1.10.0')
}

compileJava.javaCompiler.set(
        javaToolchains.compilerFor { languageVersion = JavaLanguageVersion.of(16) }
)

tasks.build.dependsOn SpigotReobfPlugin.TASK_REOBF_SPIGOT

parent.tasks.named('shadowJar', ShadowJar) {
    it.from(tasks.getByName(SpigotReobfPlugin.TASK_REOBF_SPIGOT))
}
