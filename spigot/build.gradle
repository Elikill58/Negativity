import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'io.github.goooler.shadow'
}

evaluationDependsOn(':common')

sourceSets {
    main {
        java.srcDirs = ['src']
        resources.srcDirs = ['resources']
    }
}

repositories {
    maven {
        name 'sonatype'
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
    maven {
        name 'spigot'
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
}

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

configurations {
    spigot
    compileClasspath.extendsFrom spigot
}

dependencies {
    api project(':common')
    compileOnly 'com.comphenix.protocol:ProtocolLib:4.7.0'
    spigot 'org.spigotmc:spigot-api:1.13-R0.1-SNAPSHOT'
}

processResources {
    def props = [version: version]
    inputs.properties(props)
    // Do not try to expand service files, they may contain a dollar sign to mark inner classes
    filesNotMatching('META-INF/services/*') {
        expand(props)
    }
}

compileJava.javaCompiler.set(
        javaToolchains.compilerFor { languageVersion = JavaLanguageVersion.of(8) }
)

shadowJar {
    archiveBaseName.set('NegativitySpigot')
    archiveAppendix.set(null)
    archiveClassifier.set(null)

    mergeServiceFiles()
    from(project(':common').tasks.shadowJar,
            project('integrations:essentials').jar.outputs.files,
            project('integrations:gadgetsmenu').jar.outputs.files,
            project('integrations:maxbans').jar.outputs.files,
            project('integrations:professionalbans').jar.outputs.files,
            project('integrations:ultrapunishments').jar.outputs.files,
            project('integrations:custombanplus').jar.outputs.files,
            project('integrations:worldguardv6').jar.outputs.files,
            project('integrations:worldguardv7').jar.outputs.files
    )
}

build.dependsOn shadowJar

parent.tasks.named('shadowJar', ShadowJar) {
    it.from(shadowJar.outputs)
}

task prepareServer(type: Copy) {
    from shadowJar
    rename { 'negativity.jar' }
    def pluginDir = System.getProperty('plugins.dir')
    if (pluginDir == null) {
        enabled(false)
    } else {
        into pluginDir
    }
}

subprojects {
    java.toolchain.languageVersion = JavaLanguageVersion.of(8)
    
    repositories {
	    maven {
	        name 'sonatype'
	        url 'https://oss.sonatype.org/content/repositories/snapshots/'
	    }
	    maven {
	        name 'spigot'
	        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
	    }
	}
}
