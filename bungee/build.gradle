import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'com.github.johnrengelman.shadow'
}

evaluationDependsOn(':common') // Projects are evaluated by alphabetical order, but common should be evaluated before bungee

sourceSets {
    main {
        java.srcDirs = ['src']
        resources.srcDirs = ['resources']
    }
}

repositories {
    maven {
        name 'sonatype'
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
    maven {
        name 'jitpack'
        url 'https://jitpack.io'
    }
}

dependencies {
    api project(':common')
    implementation 'org.bstats:bstats-bungeecord-lite:1.7'
    compileOnly 'net.md-5:bungeecord-api:1.19-R0.1-SNAPSHOT'
    compileOnly 'net.md-5:bungeecord-chat:1.19-R0.1-SNAPSHOT'
    compileOnly 'com.github.minecrafter:RedisBungee:master'
}

processResources {
    def props = [version: version]
    inputs.properties(props)
    // Do not try to expand service files, they may contain a dollar sign to mark inner classes
    filesNotMatching('META-INF/services/*') {
        expand(props)
    }
}

shadowJar {
    archiveBaseName.set('negativity')
    archiveAppendix.set('bungeecord')
    archiveClassifier.set(null)

    relocate('org.bstats.bungeecord', 'com.elikill58.negativity.bungee')

    mergeServiceFiles()
    from(project(':common').tasks.shadowJar)
}

build.dependsOn shadowJar

parent.tasks.named('shadowJar', ShadowJar) {
    it.from(shadowJar.outputs)
}

task prepareServer(type: Copy) {
    from shadowJar.outputs
    rename { 'negativity.jar' }
    def pluginDir = System.getProperty('plugins.dir')
    if (pluginDir == null) {
        enabled(false)
    } else {
        into pluginDir
    }
}
