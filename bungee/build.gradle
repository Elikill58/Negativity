import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

apply plugin: 'com.github.johnrengelman.shadow'

sourceSets {
    main {
        java.srcDirs = ['src']
        resources.srcDirs = ['resources']
    }
}

repositories {
    maven {
        name 'sonatype'
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    api project(':common')
    compileOnly 'net.md-5:bungeecord-api:1.15-SNAPSHOT'
    compileOnly 'net.md-5:bungeecord-chat:1.15-SNAPSHOT'
}

shadowJar {
    archiveBaseName.set('negativity')
    archiveAppendix.set('bungeecord')
    archiveClassifier.set(null)
}

build.dependsOn shadowJar

parent.tasks.named('shadowJar', ShadowJar) {
    it.from(shadowJar.outputs)
}

task prepareServer(type: Copy) {
    from shadowJar.outputs
    rename { 'negativity.jar' }
    def pluginDir = System.getProperty('plugins.dir')
    if (pluginDir == null) {
        enabled(false)
    } else {
        into pluginDir
    }
}
