import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'fabric-loom' version '0.12-SNAPSHOT'
    id 'com.github.johnrengelman.shadow'
}

sourceSets {
    main {
        java.srcDirs = ['src']
        resources.srcDirs = ['resources']
    }
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

archivesBaseName = 'Negativity'

dependencies {
    api project(':common')
    
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-screen-handler-api-v1:${project.fabric_version}"

    compileOnly 'io.netty:netty-all:4.1.79.Final'
}

compileJava.javaCompiler.set(
        javaToolchains.compilerFor { languageVersion = JavaLanguageVersion.of(17) }
)

processResources {
    def props = [
            "version": project.version,
            "fabric_version": project.fabric_version,
            "loader_version": project.loader_version,
            "minecraft_version": project.minecraft_version,
    ]
    inputs.properties props

    filesMatching("fabric.mod.json") {
        expand props
    }
}

java {
    withSourcesJar()
}

loom {
    serverOnlyMinecraftJar()
}

tasks.withType(JavaCompile).configureEach {
    // Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
    it.options.release = 17
}

shadowJar {
    archiveBaseName.set('negativity')
    archiveAppendix.set('fabric')
    archiveClassifier.set('dev')

    dependencies {
        it.include(it.project(':common'))
    }

    from(project(':common').tasks.shadowJar)
}

build.dependsOn shadowJar

task prepareServer(type: Copy) {
    from shadowJar.outputs
    rename { 'negativity.jar' }
    def pluginDir = System.getProperty('plugins.dir')
    if (pluginDir == null) {
        enabled(false)
    } else {
        into pluginDir
    }
}
