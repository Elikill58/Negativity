import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'io.github.goooler.shadow'
}

sourceSets {
    main {
        java.srcDirs = ['src']
        resources.srcDirs = ['resources']
    }
    test {
        java.srcDirs = ['testSrc']
        resources.srcDirs = ['testResources']
    }
}

dependencies {
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.9.0")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.9.0")
    implementation "org.yaml:snakeyaml:2.0"
    implementation "com.viaversion:opennbt:2.1"
    implementation "it.unimi.dsi:fastutil:8.5.6"
}

shadowJar {
    archiveBaseName.set('negativity')
    archiveAppendix.set('common')
    archiveClassifier.set(null)

    relocate("org.yaml.snakeyaml", "com.elikill58.deps.yaml")
    relocate("com.github.steveice10.opennbt", "com.elikill58.deps.opennbt")
    relocate("it.unimi.dsi.fastutil", "com.elikill58.deps.fastutil")
    minimize {
        include(dependency("it.unimi.dsi:fastutil:.*"))
        include(dependency("org.yaml:snakeyaml:.*"))
    }
    
    mergeServiceFiles()
    from(
            project('integrations:advancedban').jar.outputs.files,
            project('integrations:litebans').jar.outputs.files,
            project('integrations:viaversion').jar.outputs.files,
            project('integrations:floodgate').jar.outputs.files,
            project('integrations:geysermc').jar.outputs.files
    )
}

build.dependsOn shadowJar

parent.tasks.named('shadowJar', ShadowJar) {
    it.from(shadowJar.outputs)
}

test {
    useJUnitPlatform()
}

// Uncomment to enable event listeners instrumentation
//com.elikill58.negativity.build.InstrumentClassesTask.setupInstrumentation(project, sourceSets.main)
