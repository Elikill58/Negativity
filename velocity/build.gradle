import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

apply plugin: 'com.github.johnrengelman.shadow'

sourceSets {
    main {
        java.srcDirs = ['src']
        resources.srcDirs = ['resources']
    }
}

repositories {
    maven {
        name 'velocity'
        url 'https://repo.velocitypowered.com/snapshots/'
    }
}

dependencies {
    api project(':common')
    compileOnly 'com.velocitypowered:velocity-api:1.1.0'
}

shadowJar {
    archiveBaseName.set('negativity')
    archiveAppendix.set('velocity')
    archiveClassifier.set(null)
}

build.dependsOn shadowJar

parent.tasks.named('shadowJar', ShadowJar) {
    it.from(shadowJar.outputs)
}

task prepareServer(type: Copy) {
    from shadowJar.outputs
    rename { 'negativity.jar' }
    def pluginDir = System.getProperty('plugins.dir')
    if (pluginDir == null) {
        enabled(false)
    } else {
        into pluginDir
    }
}
