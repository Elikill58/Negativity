import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

import java.nio.file.Paths

plugins {
    id 'com.github.johnrengelman.shadow'
}

sourceSets {
    main {
        java.srcDirs = ['src']
        resources.srcDirs = ['resources']
    }
    spigot17 {
        java.srcDir 'spigot17'
    }
}

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

configurations {
    spigot
    compileClasspath.extendsFrom spigot
    spigot17Remapped
    spigot17CompileClasspath.extendsFrom spigot17Remapped
    spigot17Obf
    reobfMapMojang
    reobfMapSpigot
    specialSource
}

repositories {
    maven {
        name 'sonatype'
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
    maven {
        name 'spigot'
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    mavenLocal {
        metadataSources {
            mavenPom()
            artifact()
        }
        content {
            includeGroup('org.spigotmc')
        }
    }
}

dependencies {
    api project(':common')
    implementation 'org.bstats:bstats-bukkit:1.7'
    compileOnly 'com.comphenix.protocol:ProtocolLib:4.5.0'

    spigot17CompileOnly project(':common')
    spigot17CompileOnly sourceSets.main.output
    spigot17Remapped 'org.spigotmc:spigot:1.17-R0.1-SNAPSHOT:remapped-mojang'
    spigot17Obf 'org.spigotmc:spigot:1.17-R0.1-SNAPSHOT:remapped-obf'
    spigot fileTree(dir: "spigotJars", includes: ['*.jar'])

    reobfMapMojang group: 'org.spigotmc', name: 'minecraft-server', version: '1.17-R0.1-SNAPSHOT', classifier: 'maps-mojang', ext: 'txt'
    reobfMapSpigot group: 'org.spigotmc', name: 'minecraft-server', version: '1.17-R0.1-SNAPSHOT', classifier: 'maps-spigot', ext: 'csrg'
    specialSource 'net.md-5:SpecialSource:1.10.0'
}

processResources {
    def props = [version: version]
    inputs.properties(props)
    // Do not try to expand service files, they may contain a dollar sign to mark inner classes
    filesNotMatching('META-INF/services/*') {
        expand(props)
    }
}

compileSpigot17Java.javaCompiler.set(
        javaToolchains.compilerFor { languageVersion = JavaLanguageVersion.of(16) }
)

compileJava.javaCompiler.set(
        javaToolchains.compilerFor { languageVersion = JavaLanguageVersion.of(8) }
)

shadowJar {
    archiveBaseName.set('negativity')
    archiveAppendix.set('spigot')
    archiveClassifier.set(null)

    relocate('org.bstats', 'com.elikill58.negativity')

    mergeServiceFiles()
    from(project(':common').tasks.shadowJar,
            sourceSets.spigot17.output,
            project('integrations:essentials').jar.outputs.files,
            project('integrations:floodgate').jar.outputs.files,
            project('integrations:gadgetsmenu').jar.outputs.files,
            project('integrations:maxbans').jar.outputs.files,
            project('integrations:protocolsupport').jar.outputs.files,
            project('integrations:worldguard').jar.outputs.files
    )
}

// These tasks are based on the manual instructions from MD-5 given there:
// https://www.spigotmc.org/threads/spigot-bungeecord-1-17.510208/#post-4184317
task reobfSpigotPluginStep1(type: JavaExec) {
    dependsOn(shadowJar)

    classpath += configurations.specialSource
    classpath += configurations.spigot17Remapped
    main 'net.md_5.specialsource.SpecialSource'

    workingDir file(Paths.get('build', 'tmp', name))
    workingDir.mkdirs()

    File mapFile = configurations.reobfMapMojang.singleFile
    File inputFile = shadowJar.outputs.files.singleFile
    inputs.files(mapFile, inputFile)

    String outputPath = inputFile.absolutePath.replace('.jar', '-obf.jar')
    outputs.file(outputPath)

    args '--live', '-i', inputFile, '-o', outputPath, '-m', mapFile, '--reverse'
}

task reobfSpigotPlugin(type: JavaExec) {
    dependsOn(reobfSpigotPluginStep1)

    classpath += configurations.specialSource
    classpath += configurations.spigot17Obf
    main 'net.md_5.specialsource.SpecialSource'

    workingDir file(Paths.get('build', 'tmp', name))
    workingDir.mkdirs()

    File mapFile = configurations.reobfMapSpigot.singleFile
    File inputFile = reobfSpigotPluginStep1.outputs.files.singleFile
    inputs.files(mapFile, inputFile)

    String outputPath = inputFile.absolutePath.replace('-obf.jar', '.jar')
    outputs.file(outputPath)

    args '--live', '-i', inputFile, '-o', outputPath, '-m', mapFile
}

build.dependsOn reobfSpigotPlugin

parent.tasks.named('shadowJar', ShadowJar) {
    it.from(reobfSpigotPlugin.outputs)
}

task prepareServer(type: Copy) {
    from reobfSpigotPlugin
    rename { 'negativity.jar' }
    def pluginDir = System.getProperty('plugins.dir')
    if (pluginDir == null) {
        enabled(false)
    } else {
        into pluginDir
    }
}

subprojects {
    repositories {
        mavenLocal {
            metadataSources {
                mavenPom()
                artifact()
            }
            content {
                includeGroup('org.spigotmc')
            }
        }
    }
    java.toolchain.languageVersion = JavaLanguageVersion.of(8)
}
