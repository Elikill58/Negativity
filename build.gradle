import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

shadowJar {
    archiveBaseName.set('Negativity')
    archiveClassifier.set(null)
    
    mergeServiceFiles()
}


task prepareServer(type: Copy) {
    from shadowJar.outputs
    rename { 'Negativity.jar' }
    def pluginDir = System.getProperty('plugins.dir')
    if (pluginDir == null) {
        enabled(false)
    } else {
        into pluginDir
    }
}

def buildSponge7 = tasks.register('buildSponge7', ShadowJar) {
    it.archiveBaseName.set('Negativity')
    it.archiveClassifier.set('sponge7')

    it.mergeServiceFiles()

    it.manifest {
        from(project(':sponge7').tasks.named('jar').get().getManifest())
    }
    it.from(project(':bungee').tasks.named('shadowJar'))
    it.from(project(':velocity').tasks.named('shadowJar'))
    it.from(project(':spigot').tasks.named('shadowJar'))
    it.from(project(':sponge7').tasks.named('shadowJar'))
    it.exclude('com/elikill58/negativity/spigot1*/**.class')
}

build.dependsOn shadowJar, buildSponge7

tasks.named('javadoc') {
    enabled false
}

subprojects {
    apply plugin: 'java-library'

    group = 'com.elikill58'

    repositories {
        mavenCentral()
        maven {
            name 'jitpack'
            url 'https://jitpack.io/'
        }
        maven {
            name 'codemc'
            url 'https://repo.codemc.org/repository/maven-public/'
        }
        maven {
            name 'Sonatype'
            url 'https://oss.sonatype.org/content/groups/public/'
        }
        maven {
            name 'eliapp'
            url 'https://repo.eliapp.fr:8081/repository/internal/'
        }
    }

    dependencies {
        compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
        compileOnly 'org.checkerframework:checker-qual:3.7.0'
    }

    java.toolchain.languageVersion = JavaLanguageVersion.of(8)

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    tasks.withType(Javadoc) {
        if (project.name != 'common') {
            dependsOn ':common:javadoc'
        }
        options.encoding = 'UTF-8'
        setFailOnError(false) // TODO fix all javadoc errors
        destinationDir = file("${rootProject.buildDir}/docs/javadoc/${project.path.substring(1).replace(':', '-')}")
        options {
            if (project.name != 'common') {
                links '../common'
            }
            links "https://docs.oracle.com/javase/${java.toolchain.languageVersion.get().asInt()}/docs/api/"
        }
    }

    tasks.withType(ProcessResources) {
        filteringCharset = 'UTF-8'
    }
}
