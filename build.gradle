plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

shadowJar {
    archiveBaseName.set('Negativity')
    archiveClassifier.set(null)
    mergeServiceFiles()
}


task prepareServer(type: Copy) {
    from shadowJar.outputs
    rename { 'Negativity.jar' }
    def pluginDir = System.getProperty('plugins.dir')
    if (pluginDir == null) {
        enabled(false)
    } else {
        into pluginDir
    }
}

build.dependsOn shadowJar

subprojects {
    apply plugin: 'java-library'

    group = 'com.elikill58'

    repositories {
        jcenter()
        maven {
            name 'jitpack'
            url 'https://jitpack.io/'
        }
        maven {
            name 'codemc'
            url 'https://repo.codemc.org/repository/maven-public/'
        }
    }

    dependencies {
        compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
        compileOnly 'org.checkerframework:checker-qual:3.7.0'
    }

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    tasks.withType(ProcessResources) {
        filteringCharset = 'UTF-8'
    }
}
